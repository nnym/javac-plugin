allprojects {
    group = "user11681"
    version = "0.1.2"

    plugins.apply("java-library")

    repositories {
        mavenCentral()

        maven {url = "https://auoeke.jfrog.io/artifactory/maven"}
    }

    if (path in [":", ":annotations", ":core"]) {
        plugins.apply("maven-publish")

        if (project != rootProject) {
            rootProject.dependencies {
                api(project)
            }
        }

        publishing {
            publications {
                maven(MavenPublication) {
                    artifactId(project == rootProject ? rootProject.name : "${rootProject.name}-${project.name}")

                    from(components.java)
                }
            }

            repositories {
                maven {
                    url = "https://auoeke.jfrog.io/artifactory/maven"

                    credentials {
                        username(System.properties["maven.username"])
                        password(System.properties["maven.password"])
                    }
                }
            }
        }
    }
}
